repositories {
	maven {
		url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'

		content {
			includeGroup 'org.bukkit'
		}
	}
}

dependencies {
    shadow "org.bukkit:bukkit:1.12.2-R0.1-SNAPSHOT"
    compile group: 'org.bstats', name: 'bstats-bukkit-lite', version: '1.5'
    
    compile (project(':BlueMapCommon')) {
    	//exclude dependencies provided by bukkit
		exclude group: 'com.google.guava', module: 'guava'
		exclude group: 'com.google.code.gson', module: 'gson'
		exclude group: 'org.yaml', module: 'snakeyaml'
    }
}

build.dependsOn shadowJar {
	destinationDir = file '../build/release'
	archiveFileName = "BlueMap-${version}-bukkit.jar"
	
	//relocate 'com.flowpowered.math', 'de.bluecolored.shadow.flowpowered.math' //DON'T relocate this, because the API depends on it
	relocate 'com.typesafe.config', 'de.bluecolored.shadow.typesafe.config'
	relocate 'net.querz.nbt', 'de.bluecolored.shadow.querz.nbt'
	relocate 'ninja.leaping.configurate', 'de.bluecolored.shadow.ninja.leaping.configurate'
	relocate 'org.apache.commons.io', 'de.bluecolored.shadow.apache.commons.io'
	relocate 'org.apache.commons.lang3', 'de.bluecolored.shadow.apache.commons.lang3'
	relocate 'org.bstats.bukkit', 'de.bluecolored.shadow.bstats.bukkit'
}

processResources {
	from(sourceSets.main.resources.srcDirs) {
		include 'plugin.yml'
		
		expand (
			version: project.version
		)
	}
}
